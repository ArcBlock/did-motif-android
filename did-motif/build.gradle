plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'maven-publish'
  id 'signing'
}
def getArtificatId = { ->
  return "did-motif"
}
def getVersionName = { ->
  return new File(rootDir.getAbsolutePath() + '/version').text.replace("\n", "") ?:System.getenv('TAG_NAME') ?: "1.0.0"
}

group='io.arcblock.did.did_modif'

def snapshotRepositoryUrl = hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
    : "https://s01.oss.sonatype.org/content/repositories/snapshots/"

def repositoryUsername = project.findProperty("nexus.username") ?: hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""

def repositoryPassword = project.findProperty("nexus.pw") ?: hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""

android {
  compileSdk 31

  defaultConfig {
    minSdk 21
    targetSdk 31

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  configurations.all {
//    resolutionStrategy.force 'com.google.guava:guava:26.0-android'
//    resolutionStrategy.force 'com.google.guava:guava:18.0'

  }

}

dependencies {
//  api 'io.arcblock.walletkit:walletkit:0.5.18'
  api('org.bitcoinj:bitcoinj-core:0.16.1') {
    exclude group: 'com.google.protobuf'
    exclude group: 'org.bouncycastle'
  }
  api('org.web3j:core:4.6.0-android')
  api('io.arcblock.did:wallet-sdk:1.0.10') {
    exclude group: 'org.web3j'
    exclude group: 'org.bouncycastle'
  }
  implementation 'androidx.core:core-ktx:1.8.0'
  implementation 'androidx.appcompat:appcompat:1.4.1'
  implementation 'com.google.android.material:material:1.5.0'
  implementation 'com.google.guava:guava:28.2-android'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set("sources")
}

signing {
  sign publishing.publications
}

publishing {
  repositories {
    maven {
      url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
//      url = uri(snapshotRepositoryUrl)
      credentials {
        username = repositoryUsername
        password = repositoryPassword
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId 'io.arcblock.did'
      artifactId getArtificatId()
      version getVersionName()
      artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
      pom {
        name = artifactId
        description = 'A android library for generating DID identicons.'
        url = 'https://github.com/ArcBlock/DID-Motif-android'
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'ArcBlock'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ArcBlock/DID-Motif-android.git'
          developerConnection = 'scm:git:ssh://git@github.com/ArcBlock/DID-Motif-android.git'
          url = 'https://github.com/ArcBlock/DID-Motif-android'
        }

      }
    }
  }
}